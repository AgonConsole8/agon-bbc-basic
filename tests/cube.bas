   10 REM 3D CUBE EXAMPLE
   20 REM By Dean Belfield
   30 :
   40 MODE 1
   50 PRINT "3D Cube Demo"
   60 PRINT
   70 INPUT "SOLID (S) OR WIREFRAME (W)";F$: FILLED%=F$="S"
   80 INPUT "MODE (0 TO 2)";M%
  110 MODE M%
  115 VDU 29,640;512;:OD=128
  120 :
  130 REM Initialise the data
  140 READ CN%
  150 DIM X%(CN%),Y%(CN%),Z%(CN%),A%(CN%),B%(CN%)
  160 FOR I%=1 TO CN%: READ X%(I%),Y%(I%),Z%(I%):NEXT
  170 READ TS%
  180 DIM S%(TS%,5)
  190 FOR I%=1 TO TS%: READ S%(I%,1),S%(I%,2),S%(I%,3),S%(I%,4),S%(I%,5):NEXT
  200 REM Variables
  210 THETA=0:PSI=0:PHI=0
  220 XD=0:YD=0
  230 SD=1024
  240 REM Transform
  250 FOR I%=1 TO CN%
  260   REM Rotate
  270   XX=X%(I%):YY=Y%(I%):ZZ=Z%(I%)
  280   Y=YY*COS(PHI/180*PI)-ZZ*SIN(PHI/180*PI)
  290   ZZ=YY*SIN(PHI/180*PI)+ZZ*COS(PHI/180*PI)
  300   X=XX*COS(THETA/180*PI)-ZZ*SIN(THETA/180*PI)
  310   ZZ=XX*SIN(THETA/180*PI)+ZZ*COS(THETA/180*PI)
  320   XX=X*COS(PSI/180*PI)-Y*SIN(PSI/180*PI)
  330   YY=X*SIN(PSI/180*PI)+Y*COS(PSI/180*PI)
  340   REM Transform
  350   XX=XX+XD:YY=YY+YD
  360   REM Perspective
  370   A%(I%)=XX*SD/(OD-ZZ)
  380   B%(I%)=YY*SD/(OD-ZZ)
  390 NEXT
  400 REM Draw
  410 CLS
  415 I%=RND(-55)
  420 FOR I%=1 TO TS%
  425   GCOL 0,S%(I%,5)
  430   C1%=S%(I%,1):C2%=S%(I%,2):C3%=S%(I%,3):C4%=S%(I%,4)
  440   X1=A%(C1%):X2=A%(C2%):X3=A%(C3%):X4=A%(C4%)
  450   Y1=B%(C1%):Y2=B%(C2%):Y3=B%(C3%):Y4=B%(C4%)
  460   IF X1*(Y2-Y3)+X2*(Y3-Y1)+X3*(Y1-Y2) > 0 THEN GOTO 530
  480   IF FILLED%=0 THEN GOTO 520
  490   MOVE X1,Y1:MOVE X2,Y2:PLOT 85,X3,Y3
  500   MOVE X3,Y3:MOVE X4,Y4:PLOT 85,X1,Y1
  510   GOTO 530
  520   MOVE X1,Y1:PLOT 5,X2,Y2:PLOT 5,X3,Y3:PLOT 5,X4,Y4:PLOT 5,X1,Y1
  530 NEXT
  540 PHI=PHI+4
  550 THETA=THETA-1
  560 GOTO 250
  570 REM DATA
  580 DATA 8
  590 DATA -20,20,20
  600 DATA 20,20,20
  610 DATA -20,-20,20
  620 DATA 20,-20,20
  630 DATA -20,20,-20
  640 DATA 20,20,-20
  650 DATA -20,-20,-20
  660 DATA 20,-20,-20
  670 DATA 6
  680 DATA 1,2,4,3,9
  690 DATA 7,8,6,5,10
  700 DATA 2,6,8,4,11
  710 DATA 3,7,5,1,12
  720 DATA 3,4,8,7,13
  730 DATA 1,5,6,2,14
